// shadertype=glsl
#version 330 core
out vec4 FragColor;

in vec2 TexCoords;

uniform sampler2D m_gPosition;
uniform sampler2D m_gNormal;
uniform sampler2D m_gAlbedoSpec;

struct Light {
    vec3 Position;
    vec3 Color;    
    float Linear;
    float Quadratic;
};
const int NR_LIGHTS = 4;
uniform Light uni_Lights[NR_LIGHTS];
uniform vec3 uni_viewPos;

void main()
{             
    // retrieve data from gbuffer
    vec3 FragPos = texture(m_gPosition, TexCoords).rgb;
    vec3 Normal = texture(m_gNormal, TexCoords).rgb;
    vec3 Diffuse = texture(m_gAlbedoSpec, TexCoords).rgb;
    float Specular = texture(m_gAlbedoSpec, TexCoords).a;
    
    // then calculate lighting as usual
    vec3 lighting  = Diffuse * 0.1; // hard-coded ambient component
    vec3 viewDir  = normalize(uni_viewPos - FragPos);
    for(int i = 0; i < NR_LIGHTS; ++i)
    {
        // diffuse
        vec3 lightDir = normalize(uni_Lights[i].Position - FragPos);
        vec3 diffuse = max(dot(Normal, lightDir), 0.0) * Diffuse * uni_Lights[i].Color;
        // specular
        vec3 halfwayDir = normalize(lightDir + viewDir);  
        float spec = pow(max(dot(Normal, halfwayDir), 0.0), 16.0);
        vec3 specular = uni_Lights[i].Color * spec * Specular;
        // attenuation
        float distance = length(uni_Lights[i].Position - FragPos);
        float attenuation = 1.0 / (1.0 + uni_Lights[i].Linear * distance + uni_Lights[i].Quadratic * distance * distance);
        diffuse *= attenuation;
        specular *= attenuation;
        lighting += diffuse + specular;        
    }
    FragColor = vec4(lighting, 1.0);
}