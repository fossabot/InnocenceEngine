// shadertype=glsl
#version 330 core
layout (location = 0) out vec4 uni_lightPassRT0;

in vec2 TexCoords;

uniform vec3 uni_viewPos;
uniform sampler2D uni_geometryPassRT0;
uniform sampler2D uni_geometryPassRT1;
uniform sampler2D uni_geometryPassRT2;
uniform sampler2D uni_geometryPassRT3;
uniform samplerCube uni_irradianceMap;
uniform samplerCube uni_preFiltedMap;
uniform sampler2D uni_brdfLUT;

struct dirLight {
	vec3 direction;
    vec3 color;
};

struct pointLight {
	vec3 position;
	float radius;
    vec3 color;
};

const float PI = 3.14159265359;

const int NR_POINT_LIGHTS = 64;

const float MAX_REFLECTION_LOD = 4.0;

uniform dirLight uni_dirLight;

uniform pointLight uni_pointLights[NR_POINT_LIGHTS];

// ----------------------------------------------------------------------------
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
}  
// ----------------------------------------------------------------------------
vec3 CalcDirectionalLightRadiance(dirLight light, vec3 albedo, float metallic, float roughness, vec3 normal, vec3 viewPos, vec3 fragPos, vec3 F0)
{
		// calculate per-light radiance
        vec3 normalizedLightDir = normalize(-light.direction);
		vec3 normalizedViewDir = normalize(viewPos - fragPos);
        vec3 normalizedHalfwayDir = normalize(normalizedViewDir + normalizedLightDir);
        vec3 radiance = light.color;

        // Cook-Torrance BRDF
        float NDF = DistributionGGX(normal, normalizedHalfwayDir, roughness);   
        float G   = GeometrySmith(normal, normalizedViewDir, normalizedLightDir, roughness);      
        vec3 F    = fresnelSchlick(max(dot(normalizedHalfwayDir, normalizedViewDir), 0.0), F0);
           
        vec3 nominator    = NDF * G * F; 
        float denominator = 4 * max(dot(normal, normalizedViewDir), 0.0) * max(dot(normal, normalizedLightDir), 0.0);
        vec3 specular = nominator / max(denominator, 0.001);
        
        // kS is equal to Fresnel
        vec3 kS = F;
        // for energy conservation, the diffuse and specular light can't
        // be above 1.0 (unless the surface emits light); to preserve this
        // relationship the diffuse component (kD) should equal 1.0 - kS.
        vec3 kD = vec3(1.0) - kS;
        // multiply kD by the inverse metalness such that only non-metals 
        // have diffuse lighting, or a linear blend if partly metal (pure metals
        // have no diffuse light).
        kD *= 1.0 - metallic;	  

        // scale light by NdotL
        float NdotL = max(dot(normal, normalizedLightDir), 0.0);        

        // add to outgoing radiance Lo
        return ((kD * albedo / PI + specular) * radiance * NdotL);  // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
}

vec3 CalcPointLightRadiance(pointLight light, vec3 albedo, float metallic, float roughness, vec3 normal, vec3 viewPos, vec3 fragPos, vec3 F0)
{
	if(length(light.position - fragPos) < light.radius)
	{
		// calculate per-light radiance
        vec3 normalizedLightDir = normalize(light.position - fragPos);
		vec3 normalizedViewDir = normalize(viewPos - fragPos);
        vec3 normalizedHalfwayDir = normalize(normalizedViewDir + normalizedLightDir);
		float distance = length(light.position - fragPos);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = light.color * attenuation;

        // Cook-Torrance BRDF
        float NDF = DistributionGGX(normal, normalizedHalfwayDir, roughness);   
        float G   = GeometrySmith(normal, normalizedViewDir, normalizedLightDir, roughness);      
        vec3 F    = fresnelSchlick(max(dot(normalizedHalfwayDir, normalizedViewDir), 0.0), F0);
           
        vec3 nominator    = NDF * G * F; 
        float denominator = 4 * max(dot(normal, normalizedViewDir), 0.0) * max(dot(normal, normalizedLightDir), 0.0);
        vec3 specular = nominator / max(denominator, 0.001);
        
        // kS is equal to Fresnel
        vec3 kS = F;
        // for energy conservation, the diffuse and specular light can't
        // be above 1.0 (unless the surface emits light); to preserve this
        // relationship the diffuse component (kD) should equal 1.0 - kS.
        vec3 kD = vec3(1.0) - kS;
        // multiply kD by the inverse metalness such that only non-metals 
        // have diffuse lighting, or a linear blend if partly metal (pure metals
        // have no diffuse light).
        kD *= 1.0 - metallic;	  

        // scale light by NdotL
        float NdotL = max(dot(normal, normalizedLightDir), 0.0);

        // add to outgoing radiance Lo
        return ((kD * albedo / PI + specular) * radiance * NdotL);  // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
	}
	else
	{
	return vec3(0.0f, 0.0f, 0.0f);
	}
}

// ----------------------------------------------------------------------------
void main()
{	
	if(texture(uni_geometryPassRT0, TexCoords).a == 1.0)
    {
	uni_lightPassRT0.a = 0.0;
	}    
	else
	{
	vec3 FragPos	= texture(uni_geometryPassRT0, TexCoords).rgb;
	vec3 Normal		= texture(uni_geometryPassRT1, TexCoords).rgb;
	vec3 Albedo     = texture(uni_geometryPassRT2, TexCoords).rgb;
 	vec3 MRA        = texture(uni_geometryPassRT3, TexCoords).rgb;  
	float Metallic  = MRA.r;
	float Roughness = MRA.g;
    float AO        = MRA.b;
    // calculate reflectance at normal incidence; if dia-electric (like plastic) use F0 
    // of 0.04 and if it's a metal, use the albedo color as F0 (metallic workflow)    
    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, Albedo, Metallic);

    // reflectance equation
    vec3 Lo = vec3(0.0);

	Lo += CalcDirectionalLightRadiance(uni_dirLight, Albedo, Metallic, Roughness, Normal, uni_viewPos, FragPos, F0);
    
	for(int i = 0; i < NR_POINT_LIGHTS; ++i) 
    {
        Lo += CalcPointLightRadiance(uni_pointLights[i], Albedo, Metallic, Roughness, Normal, uni_viewPos, FragPos, F0);
    }

	//IBL ambient lighting
	vec3 normalizedViewDir = normalize(uni_viewPos - FragPos);	
	vec3 R = reflect(-normalizedViewDir, Normal);
    vec3 F = fresnelSchlickRoughness(max(dot(Normal, normalizedViewDir), 0.0), F0, Roughness); 
	vec3 kS = fresnelSchlick(max(dot(Normal, normalizedViewDir), 0.0), F0);
    vec3 kD = 1.0 - kS;
    kD *= 1.0 - Metallic;	  
	vec3 irradiance = texture(uni_irradianceMap, Normal).rgb;
    vec3 diffuse = irradiance * Albedo;

	// sample both the pre-filter map and the BRDF lut and combine them together as per the Split-Sum approximation to get the IBL specular part.
    vec3 prefilteredColor = textureLod(uni_preFiltedMap, R, Roughness * MAX_REFLECTION_LOD).rgb;    
    vec2 brdf  = texture(uni_brdfLUT, vec2(max(dot(Normal, normalizedViewDir), 0.0), Roughness)).rg;
    vec3 specular = prefilteredColor * (F * brdf.x + brdf.y);

    vec3 ambient = (kD * diffuse + specular) * AO;
	Lo = Lo + ambient;
	uni_lightPassRT0.rgb = Lo;
	uni_lightPassRT0.a = 1.0;
	}
}
